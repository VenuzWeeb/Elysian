buildscript {
    repositories {
        mavenCentral()
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        maven { url = 'https://files.minecraftforge.net/maven' }
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'eclipse'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

version = '0.1.1'
group = 'com.elysian.client'
archivesBaseName = 'elysian'

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = '1.12.2-14.23.0.2531'
    runDir = 'run'
    coreMod = 'com.elysian.client.mixin.MixinLoader'
    mappings = 'snapshot_20180814'
    makeObfSourceJar = true
}

repositories {
    mavenCentral()
    maven { url = 'https://repo.spongepowered.org/maven/' }
    maven { url = 'https://maven-eclipse.github.io/maven' }
    maven { url = 'https://jitpack.io' }
}

dependencies {
    compile('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        exclude module: 'commons-io'
        exclude module: 'guava'
        exclude module: 'gson'
    }
    compile('club.minnced:java-discord-rpc:2.0.2') {
        exclude module: 'jna'
    }
}

shadowJar {
    dependencies {
        include dependency('club.minnced:java-discord-rpc:2.0.2')
        include dependency('club.minnced:discord-rpc-release:v3.4.0')
        include dependency('org.javassist:javassist')
        include dependency('org.spongepowered:mixin')
    }
    exclude 'dummyThing', 'LICENSE.txt', 'org/**/*.html', 'META-INF/maven/**'
}

mixin {
    defaultObfuscationEnv searge
    sourceSets {
        main {
            ext.refMap = 'mixins.elysian.refmap.json'
        }
    }
}

reobf {
    shadowJar {
        mappingType = 'SEARGE'
        classpath = sourceSets.main.compileClasspath
    }
}

jar.finalizedBy 'reobfJar'

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
    manifest {
        attributes(
                'FMLCorePluginContainsFMLMod': 'true',
                'ForceLoadAsMod': 'true',
                'FMLCorePlugin': 'com.elysian.client.mixin.MixinLoader',
                'MixinConfigs': 'mixins.elysian.json',
                'tweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                'TweakOrder': 0,
                'FMLAT': 'elysian_at.cfg'
        )
    }
}

build.dependsOn(shadowJar)
